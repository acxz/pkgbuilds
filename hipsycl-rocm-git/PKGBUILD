# Maintainer: Aksel Alpay <aksel.alpay at uni-heidelberg dot de>
# Maintainer: acxz <akashpatel2008 at yahoo dot com>

pkgname=hipsycl-rocm-git
pkgver=r2241.aa00030b
pkgrel=1
pkgdesc="Multi-backend implementation of SYCL for CPUs and GPUs"
arch=("x86_64")
url="https://github.com/AdaptiveCpp/AdaptiveCpp"
license=("BSD")
provides=(hipsycl-rocm hipsycl sycl)
makedepends=(cmake git)
depends=(llvm lld clang boost openmp python hip-runtime-amd rocm-hip-sdk)
_pkgname=adaptivecpp
source=("${_pkgname}::git+${url}.git"
        "llvmspirv_installdir.patch")
sha256sums=('SKIP'
            '5d29e81867ecc51afb8cee586162664ea30a407b6e31f8d770427ad8c2b42bf1')


pkgver() {
    cd "${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    patch --directory="${srcdir}/${_pkgname}" --forward --strip=1 \
      --input="${srcdir}/llvmspirv_installdir.patch"
}

build() {
    mkdir -p ${srcdir}/${_pkgname}/build
    cd ${srcdir}/${_pkgname}/build

    cmake .. -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/opt/AdaptiveCpp/ROCm \
          -DWITH_CUDA_BACKEND=OFF \
          -DWITH_ROCM_BACKEND=ON \
          -DHIPCC_COMPILER=/opt/rocm/bin/hipcc \
          -DWITH_CPU_BACKEND=ON

    make
}

package() {
    cd "${srcdir}/${_pkgname}/build"
    make DESTDIR=${pkgdir} install
}
