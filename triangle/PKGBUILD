# Maintainer: eolianoe <eolianoe At GoogleMAIL DoT com>
# Maintainer: A Austin <acm.austin at gmail.com>
# Contributor: Martin Ortbauer <mortbauer@gmail.com>

pkgbase=archimedes-tools
pkgname=('triangle' 'showme')
pkgver=1.6
pkgrel=7
arch=('i686' 'x86_64')
url='http://www.cs.cmu.edu/~quake/archimedes.html'
pkgdesc='Archimedes is a set of tools, including mesh generators, for performing unstructured finite element simulations'
license=('custom')
source=('http://ftp.mcs.anl.gov/pub/petsc/externalpackages/Triangle.tar.gz')
md5sums=('d346097db96bc94bd8d653f86c85d273')
makedepends=('libx11')

build(){
  cd Triangle/src
  gcc ${CFLAGS} -DNO_PETSC_MALLOC -o triangle triangle.c -lm
  gcc ${CFLAGS} -DTRILIBRARY -DNO_PETSC_MALLOC -Wall -fpic -c triangle.c
  gcc ${CFLAGS} -shared -o libtriangle.so triangle.o

  gcc ${CFLAGS} -o showme showme.c -lX11
}

package_triangle() {
  url='http://www.cs.cmu.edu/~quake/triangle.html'
  pkgdesc='A Two-Dimensional Quality Mesh Generator and Delaunay Triangulator.'
  depends=('glibc')

  cd Triangle/src
  install -Dm755 triangle "${pkgdir}/usr/bin/triangle"
  install -Dm644 triangle.h "${pkgdir}/usr/include/triangle.h"
  install -Dm644 libtriangle.so "${pkgdir}/usr/lib/libtriangle.so"
  install -Dm644 "${srcdir}/Triangle/README" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_showme() {
  url='http://www.cs.cmu.edu/~quake/showme.html'
  pkgdesc='Show Me graphically displays the contents of geometric files, especially those generated by Triangle'
  depends=('libx11')

  cd Triangle/src
  install -Dm755 showme "${pkgdir}/usr/bin/showme"
  install -Dm644 "${srcdir}/Triangle/README" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
